plugins {
    id "java"
    id "jacoco"
    id "org.sonarqube" version "3.0"
    id 'info.solidsoft.pitest' version '1.5.1'
}

apply plugin: "java-library"
apply plugin: "maven"
apply plugin: "signing"


group "com.github.mikesafonov"
version "0.0.1"

sourceCompatibility = 1.8
[compileJava, compileTestJava]*.options*.encoding = "UTF-8"

repositories {
    mavenCentral()
}

jar {
    enabled = true
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives sourceJar
    archives javadocJar
}

signing {
    sign configurations.archives
}

jacoco {
    toolVersion = "0.8.3"
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
    }
}

dependencies{
    annotationProcessor("org.projectlombok:lombok:1.18.10")
    compileOnly("org.projectlombok:lombok:1.18.10")

    implementation 'jakarta.validation:jakarta.validation-api:2.0.2'

    testImplementation "org.junit.jupiter:junit-jupiter:5.6.0"
    testImplementation "org.assertj:assertj-core:3.15.0"
}

// Build, sign, and upload
uploadArchives {
    repositories {
        mavenDeployer {

            // Sign POM
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            // Destination
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            // Add required metadata to POM
            pom.project {
                name "TemporalPeriods4j"
                packaging "jar"
                description "TemporalPeriods4j is a library providing temporal periods to extend java.time API"
                url "https://github.com/MikeSafonov/TemporalPeriods4j"
                organization {
                    name "com.github.mikesafonov"
                    url "https://github.com/MikeSafonov"
                }
                issueManagement {
                    system "GitHub"
                    url "https://github.com/MikeSafonov/TemporalPeriods4j/issues"
                }
                licenses {
                    license {
                        name "MIT"
                        url "https://github.com/MikeSafonov/TemporalPeriods4j/blob/master/LICENSE"
                        distribution "repo"
                    }
                }
                scm {
                    url "https://github.com/MikeSafonov/TemporalPeriods4j"
                    connection "scm:git:git://github.com/MikeSafonov/TemporalPeriods4j.git"
                    developerConnection "scm:git:ssh://git@github.com:MikeSafonov/TemporalPeriods4j.git"
                }
                developers {
                    developer {
                        name "Mike Safonov"
                        organization "com.github.mikesafonov"
                        organizationUrl "https://github.com/MikeSafonov"
                    }
                }
            }
        }
    }
}

pitest {
    junit5PluginVersion = '0.12'
    targetClasses = ["com.github.mikesafonov.temporalperiods.*"]
    threads = 4
    outputFormats = ["HTML", "XML"]
    enableDefaultIncrementalAnalysis = true
    timestampedReports = false
    historyInputLocation = ".pitest/pitHistory.txt"
    historyOutputLocation = ".pitest/pitHistory.txt"
}
